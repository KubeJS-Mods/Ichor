import java.time.Instant

plugins {
	id 'java'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

def ENV = System.getenv()
version = "${project_version}-${ENV.GITHUB_RUN_NUMBER ? 'build.' + ENV.GITHUB_RUN_NUMBER : 'local.' + Instant.now().epochSecond}"
archivesBaseName = 'ichor'
group = 'dev.latvian.apps'

repositories {
	mavenCentral()
}

dependencies {
	compileOnly('org.jetbrains:annotations:23.0.0')
	implementation("org.ow2.asm:asm:9.2")
	testImplementation('org.junit.jupiter:junit-jupiter-api:5.10.2')
	testImplementation('org.junit.jupiter:junit-jupiter-engine:5.10.2')
	// testImplementation('junit:junit:4.13.2')
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion
	}
}

test {
	useJUnitPlatform()
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
}

publishing {
	publications {
		mavenIchor(MavenPublication) {
			artifactId = 'ichor'
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}